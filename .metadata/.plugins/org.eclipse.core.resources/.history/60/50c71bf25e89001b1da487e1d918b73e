import 'SimplePDL.ecore'

package simplepdl

context Process
inv warningSeverity: false
inv withMessage('Explicit message in process ' + self.name + ' (withMessage)'): false
inv errorSeverity: null

context Process
inv validName('Invalid name: ' + self.name):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]*')

context ProcessElement
def: process(): Process =
	Process.allInstances()
		->select(p | p.processElements->includes(self))
		->asSequence()->first()

context WorkSequence
inv successorAndPredecessorInSameProcess('Activities not in the same process : '
		+ self.predecessor.name + ' in ' + self.predecessor.process().name+ ' and '
		+ self.successor.name + ' in ' + self.successor.process().name
):
	self.process() = self.successor.process()
	and self.process() = self.predecessor.process()

context Process
inv notSameName('Deux activités d\'un meme processus ne peuvent pas avoir le meme nom'): 
	self.processElements
		->select(pe | pe.oclIsKindOf(WorkDefinition))
		->collect(pe | pe.oclAsType(WorkDefinition))
		->forAll(p1, p2 | p1 <> p2 implies p1.name <> p2.name)

context WorkDefinition
--Le nom d'une activité doit être composé d'au moins deux caractères
inv twoCharWorkDefinition ('Le nom d\'une activite doit etre compose d\'au moins deux caracteres'):
	self.name.size() >= 2
	
-- Le nom d'une activité ne doit être composé que de lettres, chiffres ou soulignés, un chiffre ne peut pas être première position
inv isValidNameWorkDefinition ('Invalid Name'):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]+')
	
context WorkSequence
inv notReflexiveDependencies ('Une dépendance ne peut pas être réflexive'):
	self.predecessor <> self.successor

context Ressource
inv twoCharRessources('Le nom d\'une ressource doit contenir au moins deux caractères'):
	self.name.size() >= 2

endpackage
